; revised sequence - interleaving works now for 3.5pl7!
#include <Avance.incl>
#include <Grad.incl>

define delay TIME_T2
"TIME_T2 = 40m"

define delay DEL
define delay DEL_2
define loopcounter COUNTER
"COUNTER=1"
"DEL=1s"
"DEL_2=1s"

define delay TAUB
"TAUB = 960u - p24 - 2u"
define delay COMPH
"COMPH = p1"


;define list<loopcounter> ncyc = {0}
;define list<loopcounter> ncyc = {0 1 20}
;define list<loopcounter> ncyc = {0 1 40}
;define list<loopcounter> ncyc = {0 1 2 3 4 5 6 7 8 9 10 12 14 16 18 20 24 28 32 36 40}
;define list<loopcounter> ncyc = {0 1 2 3 4 5 6 7 8 9 10 12 14 16 18 20}
;define list<loopcounter> ncyc = {0 20 1 18 2 16 3 14 4 12 5 10 6 9 7 8}
define list<loopcounter> ncyc = {0 40 1 36 2 32 3 28 4 24 5 20 6 18 7 16 8 14 9 12 10}
;define loopcounter ncyc
;"ncyc = l0" ;"ncyc=0"

;"d3=3.75m - p23 - 2u"
;"d4=3.75m - p23 - 2u - p8"
"d3=4m - p23 - 2u - 0.6366*p1"
"d4=4m - p23 - 2u - p8 - 4u"
"acqt0=0"

"d11=1m"
"d12=1m"

;carbon evolution time
"in0=inf2/2"
"d0=in0*0.5 - p8*0.5 - p1*2 - 2u - p1"

aqseq 312

1 ze 
  10m st0
2 2u do:f2
3 2u do:f2
4 d11 do:f2
# ifdef OFFRES_PRESAT
    30u fq=cnst21(bf hz):f1
# endif /*OFFRES_PRESAT*/

  20u pl9:f1
  d1 cw:f1 ph29
  20u do:f1


if "ncyc==0" goto 71
  4u
  "DEL = TIME_T2/(4*ncyc) - p8"
  4u
  "DEL_2 = DEL - p1"
  4u
  "COUNTER = ncyc - 1"
  4u
71 4u

;if "ncyc == 0"
;  {
;   "DEL = TIME_T2/4 - p8"
;   "DEL_1 = DEL - p8*0.63662*0.5"
;   "DEL_2 = DEL - p1"
;   "COUNTER = 0"
;  }
;else
;  { 
;   "DEL = TIME_T2/(4*ncyc) - p8"
;   "DEL_1 = TIME_T2/(4*ncyc) - p8 - p8*0.63662*0.5"
;   "DEL_2 = DEL - p1"
;   "COUNTER = ncyc - 2"
;  }

  1m UNBLKGRAD
  10u pl8:f2
#ifdef FLIPBACKS
  10u pl0:f1
  (p2:sp2 ph0:r):f1    ; SOLVENT SUPPRESSION FLIP-BACKS
  2u pl0:f1
  (p8 ph0):f2
  2u
  p20:gp0
#endif
  2.0m pl1:f1
  

   (p1 ph0):f1
   2u
   p23:gp3
   d3

   (p8 ph1):f2
   2u
   p24:gp4
   TAUB
   (center (p8*2 ph0):f2 (p1*2 ph0):f1)
   2u
   p24:gp4
   TAUB
   (p8 ph2):f2
;******	CPMG STARTS HERE **************
if "ncyc==0" goto 72
if "ncyc==1" goto 73

10 DEL
   (p8*2 ph5):f2
   DEL 
lo to 10 times COUNTER

73 DEL
   (p8*2 ph5):f2
   DEL_2 

72  (p1*2 ph3):f1

if "ncyc==0" goto 74
   DEL_2
   (p8*2 ph5):f2
   DEL
if "ncyc==1" goto 75

20 DEL
   (p8*2 ph5):f2
   DEL
lo to 20 times COUNTER
;****** CPMG ENDS HERE ****************
;******	carbon evolution **************
75 COMPH
   d0
   (p1 ph0 2u p1*2 ph7 2u p1 ph0):f1
   d0
   COMPH
   goto 76

74 d0
   (p1 ph0 2u p1*2 ph7 2u p1 ph0):f1
   d0

;****** back to protons ***************
76 (p8 ph4):f2
   2u
   p23:gp3
   d4 pl31:f2
   4u BLKGRAD

;start looping experiment
;  go=2 ph31 cpd2:f2
;  d11 do:f2 mc #0 to 4
;    F1QF(ncyc.inc)
;;    F2PH(calph(ph4,+90),caldel(d0,+in0))
;    F2PH(ip4, id0)
  
   goscnp ph31 cpd2:f2

   3m do:f2
   3m st ncyc.inc   ; nbl = number of CPMG points
   lo to 2 times nbl

   20u ncyc.res
   3m ipp1 ipp2 ipp3 ipp31
   lo to 3 times ns  ; CPMG innermost loop, then ns

;   1m BLKGRAD
   d11 mc #0 to 4
      F1QF()
      F2PH(calph(ph4, +90) & exec(rppall), caldel(d0,+in0))

1m BLKGRAD
exit 

ph0=0 
ph1=0 2
ph2=1 1 3 3
ph3=0 0 0 0 1 1 1 1
ph4=0
ph5=1
ph7=1
ph29=0
ph31=0 2 0 2 2 0 2 0


;pl1 : 1H hard 90
;pl8 : 13C hard 90 and CPMG (19 kHz)
;pl31 : 13C decoupling (2 kHz)
;p1 : 1H hard 90
;p2 : 1000 us, water flip-down
;p8 : 13C hard 90 (13.16 us for 19 kHz)
;p20 : purge gradient [1 ms]
;p23 : coherence selection [600 us]
;p24 : coherence selection [200 us]
;sp2 : 1H water flip-down (sinc1.1000)
;spoff2 : place on residual HDO resonance
;cpd2 : 13C decoupling, WALTZ-16
;pcpd2 : 13C decoupling (2 kHz)
;gpz0 : 10%
;gpz3 : 40%
;gpz4 : -40%
;o1p : 1H carrier on methyl resonances

